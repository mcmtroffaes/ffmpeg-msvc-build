cmake_minimum_required(VERSION 3.12)

# enable MSVC_RUNTIME_LIBRARY target property
# see https://cmake.org/cmake/help/latest/policy/CMP0091.html
if(POLICY CMP0091)
  cmake_policy(SET CMP0091 NEW)
endif()

project(ffmpeg_test)
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(FEATURES "avcodec;avformat;avisynthplus;avresample;bzip2;ffmpeg;ffplay;ffprobe;iconv;lzma;mp3lame;nvcodec;opus;postproc;snappy;soxr;swresample;theora;vpx;x264;x265;zlib" CACHE STRING "List of all features enabled through vcpkg.")
enable_testing()

function(test_for _feature _name)
  if (NOT ${_feature} IN_LIST FEATURES)
    set_tests_properties(${_name} PROPERTIES WILL_FAIL TRUE)
  endif()
endfunction()

add_subdirectory(avutil)
if ("avcodec" IN_LIST FEATURES)
  add_subdirectory(avcodec)
  add_test(NAME test_encoder_aac COMMAND test_find_encoder aac)
  add_test(NAME test_decoder_aac COMMAND test_find_decoder aac)
  add_test(NAME test_encoder_flac COMMAND test_find_encoder flac)
  add_test(NAME test_decoder_flac COMMAND test_find_decoder flac)
  add_test(NAME test_encoder_ffv1 COMMAND test_find_encoder ffv1)
  add_test(NAME test_decoder_ffv1 COMMAND test_find_decoder ffv1)

  add_test(NAME test_decoder_srt COMMAND test_open_decoder srt)
  add_test(NAME test_decoder_srt_iconv COMMAND test_open_decoder srt sub_charenc=ISO-8859-1)
  test_for("iconv" test_decoder_srt_iconv)

  add_test(NAME test_encoder_h264_nvenc COMMAND test_find_encoder h264_nvenc)
  add_test(NAME test_encoder_hevc_nvenc COMMAND test_find_encoder hevc_nvenc)
  test_for("nvcodec" test_encoder_h264_nvenc)
  test_for("nvcodec" test_encoder_hevc_nvenc)

  add_test(NAME test_encoder_hap COMMAND test_find_encoder hap)
  test_for("snappy" test_encoder_hap)
  add_test(NAME test_decoder_hap COMMAND test_find_decoder hap)

  add_test(NAME test_encoder_libmp3lame COMMAND test_find_encoder libmp3lame)
  test_for("mp3lame" test_encoder_libmp3lame)
  add_test(NAME test_decoder_mp3 COMMAND test_find_decoder mp3)

  add_test(NAME test_encoder_mpeg4 COMMAND test_find_encoder mpeg4)
  add_test(NAME test_decoder_mpeg4 COMMAND test_find_decoder mpeg4)

  add_test(NAME test_encoder_opus COMMAND test_find_encoder opus)
  add_test(NAME test_decoder_opus COMMAND test_find_decoder opus)
  test_for("swresample" test_decoder_opus)  # built-in opus decoder needs swresample

  add_test(NAME test_encoder_libopus COMMAND test_find_encoder libopus)
  add_test(NAME test_decoder_libopus COMMAND test_find_decoder libopus)
  test_for("opus" test_encoder_libopus)
  test_for("opus" test_decoder_libopus)

  add_test(NAME test_decoder_theora COMMAND test_find_decoder theora)
  add_test(NAME test_encoder_libtheora COMMAND test_find_encoder libtheora)
  test_for("theora" test_encoder_libtheora)

  add_test(NAME test_decoder_vp8 COMMAND test_find_decoder vp8)
  add_test(NAME test_decoder_vp9 COMMAND test_find_decoder vp9)

  add_test(NAME test_encoder_libvpx_vp8 COMMAND test_find_encoder libvpx)
  add_test(NAME test_decoder_libvpx_vp8 COMMAND test_find_decoder libvpx)
  add_test(NAME test_encoder_libvpx_vp9 COMMAND test_find_encoder libvpx-vp9)
  add_test(NAME test_decoder_libvpx_vp9 COMMAND test_find_decoder libvpx-vp9)
  test_for("vpx" test_encoder_libvpx_vp8)
  test_for("vpx" test_decoder_libvpx_vp8)
  test_for("vpx" test_encoder_libvpx_vp9)
  test_for("vpx" test_decoder_libvpx_vp9)

  add_test(NAME test_encoder_wavpack COMMAND test_find_encoder wavpack)
  add_test(NAME test_decoder_wavpack COMMAND test_find_decoder wavpack)

  add_test(NAME test_encoder_libx264 COMMAND test_find_encoder libx264)
  test_for("x264" test_encoder_libx264)

  add_test(NAME test_encoder_libx265 COMMAND test_find_encoder libx265)
  test_for("x265" test_encoder_libx265)
endif()
if ("avformat" IN_LIST FEATURES)
  add_subdirectory(avformat)
  add_test(NAME test_avisynthplus COMMAND test_decode "${CMAKE_CURRENT_SOURCE_DIR}/avisynth.avs" avisynth)
  test_for("avisynthplus" test_avisynthplus)
  add_test(NAME test_bzip2 COMMAND test_decode "${CMAKE_CURRENT_SOURCE_DIR}/subtitle_bzip2.mkv" matroska)
  test_for("bzip2" test_bzip2)
  add_test(NAME test_lzma COMMAND test_decode "${CMAKE_CURRENT_SOURCE_DIR}/tiff_lzma.tif" tiff_pipe)
  test_for("lzma" test_lzma)
  add_test(NAME test_zlib COMMAND test_decode "${CMAKE_CURRENT_SOURCE_DIR}/tiff_zlib.tif" tiff_pipe)
  test_for("zlib" test_zlib)
endif()
if ("postproc" IN_LIST FEATURES)
  add_subdirectory(postproc)
endif()
if ("swresample" IN_LIST FEATURES)
  add_subdirectory(swresample)
  add_test(NAME test_swresample_swr COMMAND test_find_swr_engine swr)
  add_test(NAME test_swresample_soxr COMMAND test_find_swr_engine soxr)
  test_for("soxr" test_swresample_soxr)
endif()
if ("avresample" IN_LIST FEATURES)
  add_subdirectory(avresample)
endif()
if ("ffmpeg" IN_LIST FEATURES)
  add_test(NAME test_tools_ffmpeg COMMAND "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/tools/ffmpeg/ffmpeg" -h)
endif()
if ("ffplay" IN_LIST FEATURES)
  add_test(NAME test_tools_ffplay COMMAND "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/tools/ffmpeg/ffplay" -h)
endif()
if ("ffprobe" IN_LIST FEATURES)
  add_test(NAME test_tools_ffprobe COMMAND "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/tools/ffmpeg/ffprobe" -h)
endif()