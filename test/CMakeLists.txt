cmake_minimum_required(VERSION 3.12)

# enable MSVC_RUNTIME_LIBRARY target property
# see https://cmake.org/cmake/help/latest/policy/CMP0091.html
if(POLICY CMP0091)
  cmake_policy(SET CMP0091 NEW)
endif()

project(ffmpeg_test)
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(FEATURES "avcodec;avdevice;avformat;avfilter;avisynthplus;bzip2;ffmpeg;ffplay;ffprobe;ass;dav1d;freetype;iconv;ilbc;lzma;modplug;mp3lame;nvcodec;opencl;opengl;openh264;openjpeg;opus;postproc;sdl2;snappy;soxr;speex;ssh;swresample;tesseract;theora;vorbis;vpx;webp;x264;x265;xml2;zlib" CACHE STRING "List of all features enabled through vcpkg.")
include(CTest)

function(test_for _feature _name)
  if (NOT ${_feature} IN_LIST FEATURES)
    set_tests_properties(test_cmake_${_name} PROPERTIES WILL_FAIL TRUE)
    set_tests_properties(test_pkgconfig_${_name} PROPERTIES WILL_FAIL TRUE)
  endif()
endfunction()

function(register_test)
  cmake_parse_arguments(arg "" "NAME" "COMMAND" ${ARGN})
  add_test(NAME test_cmake_${arg_NAME} COMMAND test_cmake_${arg_COMMAND})
  add_test(NAME test_pkgconfig_${arg_NAME} COMMAND test_pkgconfig_${arg_COMMAND})
endfunction()

add_subdirectory(cmake/avutil)
add_subdirectory(pkgconfig/avutil)
if ("avcodec" IN_LIST FEATURES)
  add_subdirectory(cmake/avcodec)
  add_subdirectory(pkgconfig/avcodec)
  register_test(NAME avcodec_encoder_aac COMMAND avcodec_find_encoder aac)
  register_test(NAME avcodec_decoder_aac COMMAND avcodec_find_decoder aac)
  register_test(NAME avcodec_encoder_flac COMMAND avcodec_find_encoder flac)
  register_test(NAME avcodec_decoder_flac COMMAND avcodec_find_decoder flac)
  register_test(NAME avcodec_encoder_ffv1 COMMAND avcodec_find_encoder ffv1)
  register_test(NAME avcodec_decoder_ffv1 COMMAND avcodec_find_decoder ffv1)

  register_test(NAME avcodec_decoder_libdav1d COMMAND avcodec_find_decoder libdav1d)
  test_for("dav1d" avcodec_decoder_libdav1d)

  register_test(NAME avcodec_decoder_srt COMMAND avcodec_open_decoder srt)
  register_test(NAME avcodec_decoder_srt_iconv COMMAND avcodec_open_decoder srt sub_charenc=ISO-8859-1)
  test_for("iconv" avcodec_decoder_srt_iconv)

  register_test(NAME avcodec_encoder_libilbc COMMAND avcodec_find_encoder libilbc)
  register_test(NAME avcodec_decoder_libilbc COMMAND avcodec_find_decoder libilbc)
  test_for("ilbc" avcodec_encoder_libilbc)
  test_for("ilbc" avcodec_decoder_libilbc)

  register_test(NAME avcodec_encoder_h264_nvenc COMMAND avcodec_find_encoder h264_nvenc)
  register_test(NAME avcodec_encoder_hevc_nvenc COMMAND avcodec_find_encoder hevc_nvenc)
  test_for("nvcodec" avcodec_encoder_h264_nvenc)
  test_for("nvcodec" avcodec_encoder_hevc_nvenc)

  register_test(NAME avcodec_encoder_hap COMMAND avcodec_find_encoder hap)
  test_for("snappy" avcodec_encoder_hap)
  register_test(NAME avcodec_decoder_hap COMMAND avcodec_find_decoder hap)

  register_test(NAME avcodec_encoder_libmp3lame COMMAND avcodec_find_encoder libmp3lame)
  test_for("mp3lame" avcodec_encoder_libmp3lame)
  register_test(NAME avcodec_decoder_mp3 COMMAND avcodec_find_decoder mp3)

  register_test(NAME avcodec_encoder_mpeg4 COMMAND avcodec_find_encoder mpeg4)
  register_test(NAME avcodec_decoder_mpeg4 COMMAND avcodec_find_decoder mpeg4)

  register_test(NAME avcodec_encoder_opus COMMAND avcodec_find_encoder opus)
  register_test(NAME avcodec_decoder_opus COMMAND avcodec_find_decoder opus)
  test_for("swresample" avcodec_decoder_opus)  # built-in opus decoder needs swresample

  register_test(NAME avcodec_encoder_libopenh264 COMMAND avcodec_find_encoder libopenh264)
  register_test(NAME avcodec_decoder_libopenh264 COMMAND avcodec_find_decoder libopenh264)
  test_for("openh264" avcodec_encoder_libopenh264)
  test_for("openh264" avcodec_decoder_libopenh264)

  register_test(NAME avcodec_encoder_libopenjpeg COMMAND avcodec_find_encoder libopenjpeg)
  register_test(NAME avcodec_decoder_libopenjpeg COMMAND avcodec_find_decoder libopenjpeg)
  test_for("openjpeg" avcodec_encoder_libopenjpeg)
  test_for("openjpeg" avcodec_decoder_libopenjpeg)

  register_test(NAME avcodec_encoder_libopus COMMAND avcodec_find_encoder libopus)
  register_test(NAME avcodec_decoder_libopus COMMAND avcodec_find_decoder libopus)
  test_for("opus" avcodec_encoder_libopus)
  test_for("opus" avcodec_decoder_libopus)

  register_test(NAME avcodec_encoder_libspeex COMMAND avcodec_find_encoder libspeex)
  register_test(NAME avcodec_decoder_libspeex COMMAND avcodec_find_decoder libspeex)
  test_for("speex" avcodec_encoder_libspeex)
  test_for("speex" avcodec_decoder_libspeex)

  register_test(NAME avcodec_decoder_theora COMMAND avcodec_find_decoder theora)
  register_test(NAME avcodec_encoder_libtheora COMMAND avcodec_find_encoder libtheora)
  test_for("theora" avcodec_encoder_libtheora)

  register_test(NAME avcodec_decoder_vp8 COMMAND avcodec_find_decoder vp8)
  register_test(NAME avcodec_decoder_vp9 COMMAND avcodec_find_decoder vp9)

  register_test(NAME avcodec_encoder_libvorbis COMMAND avcodec_find_encoder libvorbis)
  register_test(NAME avcodec_decoder_libvorbis COMMAND avcodec_find_decoder libvorbis)
  test_for("vorbis" avcodec_encoder_libvorbis)
  test_for("vorbis" avcodec_decoder_libvorbis)

  register_test(NAME avcodec_encoder_libvpx_vp8 COMMAND avcodec_find_encoder libvpx)
  register_test(NAME avcodec_decoder_libvpx_vp8 COMMAND avcodec_find_decoder libvpx)
  register_test(NAME avcodec_encoder_libvpx_vp9 COMMAND avcodec_find_encoder libvpx-vp9)
  register_test(NAME avcodec_decoder_libvpx_vp9 COMMAND avcodec_find_decoder libvpx-vp9)
  test_for("vpx" avcodec_encoder_libvpx_vp8)
  test_for("vpx" avcodec_decoder_libvpx_vp8)
  test_for("vpx" avcodec_encoder_libvpx_vp9)
  test_for("vpx" avcodec_decoder_libvpx_vp9)

  register_test(NAME avcodec_encoder_wavpack COMMAND avcodec_find_encoder wavpack)
  register_test(NAME avcodec_decoder_wavpack COMMAND avcodec_find_decoder wavpack)

  register_test(NAME avcodec_encoder_libwebp COMMAND avcodec_find_encoder libwebp)
  test_for("webp" avcodec_encoder_libwebp)

  register_test(NAME avcodec_encoder_libx264 COMMAND avcodec_find_encoder libx264)
  test_for("x264" avcodec_encoder_libx264)

  register_test(NAME avcodec_encoder_libx265 COMMAND avcodec_find_encoder libx265)
  test_for("x265" avcodec_encoder_libx265)
endif()
if ("avfilter" IN_LIST FEATURES)
  add_subdirectory(cmake/avfilter)
  add_subdirectory(pkgconfig/avfilter)
  register_test(NAME avfilter_filter_avgblur COMMAND avfilter_find_filter avgblur)

  register_test(NAME avfilter_filter_ass COMMAND avfilter_find_filter ass)
  test_for("ass" avfilter_filter_ass)

  register_test(NAME avfilter_filter_drawtext COMMAND avfilter_find_filter drawtext)
  test_for("freetype" avfilter_filter_drawtext)

  register_test(NAME avfilter_filter_ocr COMMAND avfilter_find_filter ocr)
  test_for("tesseract" avfilter_filter_ocr)

  register_test(NAME avfilter_filter_avgblur_opencl COMMAND avfilter_find_filter avgblur_opencl)
  test_for("opencl" avfilter_filter_avgblur_opencl)
endif()
if ("avformat" IN_LIST FEATURES)
  add_subdirectory(cmake/avformat)
  add_subdirectory(pkgconfig/avformat)

  register_test(NAME avformat_decode_avisynthplus COMMAND avformat_decode "${CMAKE_CURRENT_SOURCE_DIR}/avisynth.avs" avisynth)
  test_for("avisynthplus" avformat_decode_avisynthplus)
  if(WIN32)
    register_test(NAME avformat_decode_avisynthplus2 COMMAND avformat_decode "${CMAKE_CURRENT_SOURCE_DIR}/avisynth2.avs" avisynth)
    test_for("avisynthplus" avformat_decode_avisynthplus2)
  endif()
  register_test(NAME avformat_decode_bzip2 COMMAND avformat_decode "${CMAKE_CURRENT_SOURCE_DIR}/subtitle_bzip2.mkv" matroska)
  test_for("bzip2" avformat_decode_bzip2)
  register_test(NAME avformat_decode_lzma COMMAND avformat_decode "${CMAKE_CURRENT_SOURCE_DIR}/tiff_lzma.tif" tiff_pipe)
  test_for("lzma" avformat_decode_lzma)
  register_test(NAME avformat_decode_zlib COMMAND avformat_decode "${CMAKE_CURRENT_SOURCE_DIR}/tiff_zlib.tif" tiff_pipe)
  test_for("zlib" avformat_decode_zlib)

  register_test(NAME avformat_input_dash  COMMAND avformat_find_format "input"  "dash")
  register_test(NAME avformat_output_dash COMMAND avformat_find_format "output" "dash")
  test_for("xml2" avformat_input_dash)
  register_test(NAME avformat_input_libmodplug COMMAND avformat_find_format "input" "libmodplug")
  test_for("modplug" avformat_input_libmodplug)

  register_test(NAME avformat_protocol_input_file  COMMAND avformat_find_protocol "input"  "file")
  register_test(NAME avformat_protocol_output_file COMMAND avformat_find_protocol "output" "file")
  register_test(NAME avformat_protocol_input_sftp  COMMAND avformat_find_protocol "input"  "sftp")
  register_test(NAME avformat_protocol_output_sftp COMMAND avformat_find_protocol "output" "sftp")
  test_for("ssh" avformat_protocol_input_sftp)
  test_for("ssh" avformat_protocol_output_sftp)
endif()
if ("postproc" IN_LIST FEATURES)
  add_subdirectory(cmake/postproc)
  add_subdirectory(pkgconfig/postproc)
endif()
if ("swresample" IN_LIST FEATURES)
  add_subdirectory(cmake/swresample)
  add_subdirectory(pkgconfig/swresample)
  register_test(NAME swresample_swr COMMAND swresample_find_swr_engine swr)
  register_test(NAME swresample_soxr COMMAND swresample_find_swr_engine soxr)
  test_for("soxr" swresample_soxr)
endif()
if ("avdevice" IN_LIST FEATURES)
  add_subdirectory(cmake/avdevice)
  add_subdirectory(pkgconfig/avdevice)
  register_test(NAME avdevice_opengl COMMAND avdevice_find_output_video_device opengl)
  test_for("opengl" avdevice_opengl)
  register_test(NAME avdevice_sdl2 COMMAND avdevice_find_output_video_device sdl2)
  test_for("sdl2" avdevice_sdl2)
endif()
if ("ffmpeg" IN_LIST FEATURES)
  add_test(NAME test_tools_ffmpeg COMMAND "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/tools/ffmpeg/ffmpeg" -h)
endif()
if ("ffplay" IN_LIST FEATURES)
  add_test(NAME test_tools_ffplay COMMAND "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/tools/ffmpeg/ffplay" -h)
endif()
if ("ffprobe" IN_LIST FEATURES)
  add_test(NAME test_tools_ffprobe COMMAND "${_VCPKG_INSTALLED_DIR}/${VCPKG_TARGET_TRIPLET}/tools/ffmpeg/ffprobe" -h)
endif()
